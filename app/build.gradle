apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "privacyfriendlyshoppinglist.secuso.org.privacyfriendlyshoppinglist"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 8
        versionName "1.1"
        multiDexEnabled true

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }

    sourceSets {
        main {
            res.srcDirs =
                    [
                            'src/main/res/layouts/about',
                            'src/main/res/layouts/baseactivity',
                            'src/main/res/layouts/help',
                            'src/main/res/layouts/main',
                            'src/main/res/layouts/preferences',
                            'src/main/res/layouts/products',
                            'src/main/res/layouts/settings',
                            'src/main/res/layouts/deletelists',
                            'src/main/res/layouts/deleteproducts',
                            'src/main/res/layouts/shared',
                            'src/main/res/layouts/shoppinglist',
                            'src/main/res/layouts/statistics',
                            'src/main/res/layouts/camera',
                            'src/main/res/layouts/tutorials',
                            'src/main/res'
                    ]
        }
    }
    lint {
        lintConfig = file("lint.xml")
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        def appName = "pfa-shopping-list"
        outputFileName = appName + "-${variant.name}-v${variant.versionName}.apk"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // android
    androidTestImplementation 'androidx.annotation:annotation:1.0.0'

    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'

    // tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.6'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    testImplementation 'pl.pragmatists:JUnitParams:0.3.6'
    // dependency injection
    implementation 'com.squareup.dagger:dagger:1.2.5'
    annotationProcessor 'com.squareup.dagger:dagger-compiler:1.2.5'
    compileOnly 'com.squareup.dagger:dagger-compiler:1.2.5'

    // persistence
    implementation 'com.j256.ormlite:ormlite-android:5.0'
    // reactive x
    implementation 'io.reactivex:rxandroid:1.2.0'
    implementation 'io.reactivex:rxjava:1.1.5'
    // joda
    implementation 'joda-time:joda-time:2.10.1'
    // graphics
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.0-beta1'
    // image zoom
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.5.0'

    implementation 'androidx.sqlite:sqlite:2.3.0'
    implementation 'androidx.sqlite:sqlite-ktx:2.3.0'

    // Backup API
    implementation project(':backup-api')
    def work_version = '2.7.1'
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"

    implementation 'androidx.core:core-ktx:1.7.20'

    // Multi-Dex
    def multidex_version = "2.0.1"
    implementation("androidx.multidex:multidex:$multidex_version")
}

